# 函式

本章節包含了基本的 Elm 語法，重要必須熟悉：函式，函式標記式（function signatures），部份套用（partial application）及輸送運算子（pipe operator）。

## 函式

Elm 支援兩種函式：

- 匿名函式（anonymous functions）
- 具名函式（named functions）

### 匿名函式

顧名思義，沒有函式名稱的函式：

```elm
\x -> x + 1

\x y -> x + y
```

反斜線與箭頭之間為函式的引數（傳給函式的值），箭頭右邊則是你希望作的事情。

### 具名函式

Elm 的具名函式看起來像似：

```elm
add1 : Int -> Int
add1 x =
  x + 1
```

- 範例中第一行為函式的標記式。雖然標記式是選擇性的（optional）加入，但建議加入。這讓你的函式的意圖更加清楚。
- 剩下的部份則是函式的實作。實作必須遵循標記式。

此例中標記式描述：給定一個整數（Int）作為引數，傳回另一個整數。

呼叫方法類似：

```
add1 3
```

Elm 使用*空白*來表示函式套用（而非括號）

下面是另一個具名函式：

```elm
add : Int -> Int -> Int
add x y =
  x + y
```

此函式取得兩個引數（皆為 Int），傳回另一個 Int。呼叫如下：

```elm
add 2 3
```

### 無引數

無引數的函式是一個常數：

```elm
name =
  "Sam"
```

### 如何套用函式

上述取得兩個引數的函式像是：

```elm
divide : Float -> Float -> Float
divide x y =
    x / y
```

我們可以想做這個標記式為一個函式，取得兩個浮點數的引數，傳回另一個浮點數：

```elm
divide 5 2 == 2.5
```

然而，這並不完全正確，Elm 中所有函式都只取得一個引數，傳回一個結果。結果可以是另一個函式。
讓我用上述函式解釋。

```elm
-- 當我們用：

divide 5 2

-- 這會被核定成：

((divide 5) 2)

-- 首先，`divide 5` 會先求值。
-- 引數 `5` 套用到 `divide`，傳回一個中間的函式。

divide 5 -- -> 中間的函式

-- 接著呼叫這個中間的函式 `divide5`。
-- 如果看得到中間函式的標記式，那會長的像似：

divide5 : Float -> Float
divide5 y =
  5 / y

-- 會有個函式已經套用了 `5`。

-- 接著，下一個引數套用，舉例 `2`

divide5 2

-- 這將會傳回最後結果
```

避免撰寫括號的原因就是因為套用函式是**從左聯繫**。

### 括號用來分組（Grouping）

當你需要呼叫函式，引數的值來自另一個函式傳回的結果，你需要括號來分組呼叫：

```elm
add 1 (divide 12 3)
```

這裡 `divide 12 3` 傳回的結果，帶入 `add` 的第二個引數。

相較之下，其他語言可能會寫成：

```js
add(1, divide(12, 3))
```

## 部份套用（Partial application）

如上述說明，每個函式只取一個引數，傳回另一個函式或者結果。k.
表示你呼叫類似上述 `add` 函式並帶入一個引數，例如 `add 2`，傳回的是*部份套用的函式*。
傳回的函式標記式為 `Int -> Int`。

`add 2` 傳回另一個函式，其中 `2` 的值綁定為第一個引數值。
呼叫傳回的函式並帶入第二個引數值，傳回 `2 +` 第二個引數值的結果：

```elm
add2 = add 2
add2 3 -- 結果為 5
```

部份套用在 Elm 非常地有用，讓你的程式碼更容易閱讀，並在應用程式內的函式之間傳遞狀態（state）。

## 輸送運算子（The pipe operator）

如上述所示，你可以呼叫巢狀函式，像是：

```elm
add 1 (multiply 2 3)
```

這只是個平凡的範例，但考慮到更複雜的範例：

```elm
sum (filter (isOver 100) (map getCost records))
```

這樣的程式碼難以閱讀，因為需要從裡到外解決。輸送運算子允許撰寫出更好閱讀的方式：

```elm
3
    |> multiply 2
    |> add 1
```

這十分依賴先前提到的部份套用。這個範例中，`3` 這個值傳遞到部份套用函式 `multiply 2`。接著，其結果值再傳遞到部份套用函式 `add 1`。

若使用輸送運算子，上述複雜的範例可寫成：

```elm
records
    |> map getCost
    |> filter (isOver 100)
    |> sum
```
